/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/******************************************************************************************
        Item: Metal Door
        Locked:  Yes
        Trapped: No
        Close:   No
        Description: Slave doors in NCR entrance

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Using template

           Updated:
******************************************************************************************/


/* Include Files */
#define SCRIPT_REALNAME "sislvdor"
#include "../headers/define.h"
#define NAME                    SCRIPT_SISLVDOR
#include "../headers/command.h"

#include "../headers/ncr.h"
#include "../headers/ncrent.h"



/* Sets whether the door is locked or trapped */
#define LOCKED_STATUS                   STATE_ACTIVE
#define TRAPPED_STATUS                  STATE_INACTIVE
#define DOOR_STATUS                     STATE_METAL
#define LOCK_STATUS                     STATE_STANDARD_LOCK

/* Penalties for Lock difficulty based on whether or not you are using lockpicks. */
#define Lock_Bonus                      (-20)







#define NUM_OF_PENS                     (13)

procedure destroy_p_proc;



#define custom_use_obj_on_p_proc
#include "../headers/doors.h"

/* Slave doors */
   /* group 1*/
import variable slvdor_g1d1;
import variable slvdor_g1d2;
import variable slvdor_g1d3;
import variable slvdor_g1d4;
   /* group 2 */
import variable slvdor_g2d1;
import variable slvdor_g2d2;
import variable slvdor_g2d3;
   /* group 3 */
import variable slvdor_g3d1;
import variable slvdor_g3d2;
import variable slvdor_g3d3;
   /* group 4 */
import variable slvdor_g4d1;
import variable slvdor_g4d2;
import variable slvdor_g4d3;
/* end slave doors */








/********************************************************************
   Any time that a critter tries to use this door will call this
   procedure. it will check to see if the door is trapped and locked.
********************************************************************/

procedure use_p_proc begin
/* Trap_Roll is a global variable to this script, defined at the beginning
   of the script. */

   ndebug("source_obj: " + source_obj + " dude_obj: " + dude_obj);
   if (obj_in_party(source_obj)) then begin
      ndebug("object in party");
      Traps_Roll:=roll_vs_skill(source_obj,SKILL_TRAPS,Trap_Bonus);

      if (local_var(LVAR_Trapped) == STATE_ACTIVE) then begin
         if (is_success(Traps_Roll)) then begin
            script_overrides;
            set_local_var(LVAR_Found_Trap,1);
            reg_anim_clear(source_obj);

            if (source_obj == dude_obj) then begin
                  display_msg(door_mstr(204));
            end
            else begin
                  display_msg(door_mstr(205));
            end
         end  else begin
            if (obj_is_locked(self_obj)) then begin
               script_overrides;
               display_msg(door_mstr(203));
               call Damage_Critter;
            end
            else begin
               call Damage_Critter;
            end
         end
      end else begin
         script_overrides;
         display_msg(mstr(213));
      end
   end else begin
      script_overrides;
   end
end

/**********************************************************************************
   Should the player examine the door closely, then he should be allowed a lockpick
   roll, a traps roll and a perception roll. Depending on which rolls are made will
   determine how much information about the door will be given.
**********************************************************************************/

procedure description_p_proc begin
   script_overrides;
   if ((local_var(LVAR_Locked) == STATE_ACTIVE) and (local_var(LVAR_Trapped) == STATE_ACTIVE)) then begin
       call Look_Traps_And_Locks;
   end

   else if (local_var(LVAR_Trapped) == STATE_ACTIVE) then begin
       call Look_Traps;
   end

   else if (local_var(LVAR_Locked) == STATE_ACTIVE) then begin
       call Look_Locks;
   end

   else if (DOOR_STATUS == STATE_WOOD) then begin
       display_msg(door_mstr(100));
   end

   else begin
       display_msg(door_mstr(101));
   end
end


/**********************************************************************************
   Should the player try using a skill on the door, here are where the skill rolls
   will be made. The ones which need to be in here are: Lockpick and Traps. Science
   and Repair can be added to this list to give more information about the door.
**********************************************************************************/

procedure use_skill_on_p_proc begin
   variable Skill_Used;

   Skill_Used:=action_being_used;

   if (Skill_Used == SKILL_LOCKPICK) then begin
      script_overrides; //added by killap
      display_msg(mstr(212));
/*
       if (local_var(LVAR_Locked) == STATE_ACTIVE) then begin
           call Unlock_Lock;
       end
       else begin
           call Set_Lock;
       end
*/
   end                                                          // End of Skill_Lockpick

   else if (Skill_Used == SKILL_TRAPS) then begin
       if (local_var(LVAR_Trapped) == STATE_ACTIVE) then begin
           call Skill_Disarm_Traps;
       end
       else begin
           script_overrides;
           if (source_obj == dude_obj) then begin
               display_msg(door_mstr(195));
           end

           else begin
               display_msg(obj_name(source_obj)+door_mstr(200));
           end
       end
   end                                                          // End of Skill_Traps

end


/**********************************************************************************
   This is called when the player is using an object on the door. When the check is
   made to find out what is being used, obj_pid(obj_being_used_with) will need to
   be checked against a prototype.
**********************************************************************************/
procedure use_obj_on_p_proc begin
   variable Tool;

   Tool:=obj_pid(obj_being_used_with);
   if ((Tool == PID_DYNAMITE) or (Tool == PID_PLASTIC_EXPLOSIVES)) then begin
       script_overrides; //added by killap
       call Set_Trap;
   end else begin
      script_overrides;
      display_msg(mstr(212));
   end
end


/******************************************************************************************
   Should the door sustain damage from anything (ie, dynamite, plastic explosives, rockets,
   or any other forms of damage), this procedure will be called to destroy the door and
   free up the space.
******************************************************************************************/

procedure damage_p_proc begin
   variable Trap_Damage;

  /************ Wood Door ****************/

 if (weapon_dmg_type(target_obj) == DMG_explosion) then begin //added by killap
   if (DOOR_STATUS == STATE_WOOD) then begin
       if (local_var(LVAR_Trapped) == STATE_ACTIVE) then begin
           set_local_var(LVAR_Trapped, STATE_INACTIVE);
           set_local_var(LVAR_Locked, STATE_INACTIVE);
           Trap_Damage:=random(MIN_DAMAGE,MAX_DAMAGE);
           explosion(self_tile, self_elevation, Trap_Damage);
           destroy_object(self_obj);
       end

       else begin
           set_local_var(LVAR_Trapped, STATE_INACTIVE);
           set_local_var(LVAR_Locked, STATE_INACTIVE);
           destroy_object(self_obj);
       end
   end

  /************ Metal Door ****************/

   else if (DOOR_STATUS == STATE_METAL) then begin
       set_local_var(LVAR_Explosion_Attempts,local_var(LVAR_Explosion_Attempts)+1);
       if (local_var(LVAR_Trapped) == STATE_ACTIVE) then begin
           set_local_var(LVAR_Locked, STATE_INACTIVE);
           Trap_Damage:=random(MIN_DAMAGE,MAX_DAMAGE);
           explosion(self_tile, self_elevation, Trap_Damage);
       end

       else if (local_var(LVAR_Explosion_Attempts) > DOOR_STRENGTH) then begin
           set_local_var(LVAR_Trapped, STATE_INACTIVE);
           set_local_var(LVAR_Locked,STATE_INACTIVE);
           destroy_object(self_obj);
       end
   end
 end

   else begin
       display_msg(door_mstr(193));
   end

  /************ Wood Door ****************/

end

//added by killap
procedure destroy_p_proc begin
   inc_map_var(MVAR_Pens_Destroyed);
   if (map_var(MVAR_Pens_Destroyed) == NUM_OF_PENS) then begin
      ncr_set_global_state(NCR_SLAVES_FREE);
      set_map_var(MVAR_NCR_ENT_SLAVES_FREED, 1);
      set_map_var(MVAR_Slavers_Attack, 1);
   end
end
//end added

/***************************************************************************************
   Whenever the map is first entered, this procedure will be called. The main purpose of
   this procedure is to lock the door from the outset, rather than having to worry about
   it through Mapper. After the door has been unlocked, it will remain as such, unless
   the player locks it once more.
***************************************************************************************/

procedure map_enter_p_proc begin

/* Set up the door state when the player first enters the map */
   if (local_var(LVAR_Set_Door_Status) == 0) then begin
       set_local_var(LVAR_Set_Door_Status,1);
       set_local_var(LVAR_Locked,LOCKED_STATUS);
       set_local_var(LVAR_Trapped,TRAPPED_STATUS);
   end

   if (local_var(LVAR_Locked) == STATE_ACTIVE) then begin
       obj_lock(self_obj);
   end

   else begin
       obj_unlock(self_obj);
   end
   if (self_tile == SLVDORG1D1) then
      set_slave_door(g1d1);
   else if (self_tile == SLVDORG1D2) then
      set_slave_door(g1d2);
   else if (self_tile == SLVDORG1D3) then
      set_slave_door(g1d3);
   else if (self_tile == SLVDORG1D4) then
      set_slave_door(g1d4);
   else if (self_tile == SLVDORG2D1) then
      set_slave_door(g2d1);
   else if (self_tile == SLVDORG2D2) then
      set_slave_door(g2d2);
   else if (self_tile == SLVDORG2D3) then
      set_slave_door(g2d3);
   else if (self_tile == SLVDORG3D1) then
      set_slave_door(g3d1);
   else if (self_tile == SLVDORG3D2) then
      set_slave_door(g3d2);
   else if (self_tile == SLVDORG3D3) then
      set_slave_door(g3d3);
   else if (self_tile == SLVDORG4D1) then
      set_slave_door(g4d1);
   else if (self_tile == SLVDORG4D2) then
      set_slave_door(g4d2);
   else if (self_tile == SLVDORG4D3) then
      set_slave_door(g4d3);
end


/**************************************************************************************
   This procedure gets called roughly every 30 seconds of real time. It is used to make
   sure that the door does not lock on it's own and that the player will be able to get
   through once he has unlocked the door. Once more, this is so that locking and
   unlocking do not need to be done in Mapper.
**************************************************************************************/

procedure map_update_p_proc begin
   if (local_var(LVAR_Locked) == STATE_ACTIVE) then begin
       obj_lock(self_obj);
   end

   else begin
       obj_unlock(self_obj);
   end
end
